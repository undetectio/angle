# Copyright 2021 The ANGLE Project Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
#
# Build configuration for OpenCL-related targets.

import("../../gni/angle.gni")

assert(angle_enable_cl)

angle_shared_library("OpenCL_ANGLE") {
  defines = [ "LIBCL_IMPLEMENTATION" ]

  sources = [
    "dispatch.cpp",
    "dispatch.h",
    "libOpenCL_autogen.cpp",
  ]

  configs += [
    "$angle_root:debug_annotations_config",
    "$angle_root:library_name_config",
  ]

  deps = [
    "$angle_root:angle_common",
    "$angle_root:cl_includes",
  ]
}

icd_library_name = "OpenCL_ICD_ANGLE"

config("opencl_icd_library_name") {
  if (is_win) {
    defines = [ "ANGLE_OPENCL_ICD_LIB_NAME=\"" + icd_library_name + "\"" ]
  } else {
    defines = [ "ANGLE_OPENCL_ICD_LIB_NAME=\"lib" + icd_library_name + ".so\"" ]
  }
}

angle_shared_library(icd_library_name) {
  defines = [ "LIBCL_IMPLEMENTATION" ]

  sources = [
    "dispatch.cpp",
    "dispatch.h",
    "libOpenCL_ICD.cpp",
  ]

  configs += [
    "$angle_root:debug_annotations_config",
    "$angle_root:library_name_config",
  ]

  deps = [
    "$angle_root:angle_common",
    "$angle_root:cl_includes",
  ]
}

group("angle_cl") {
  data_deps = [
    ":OpenCL_ANGLE",
    ":OpenCL_ICD_ANGLE",
  ]
}
